apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'
apply plugin: 'com.jakewharton.butterknife'

android {
    signingConfigs {
        release {
            //storeFile file("../yourapp.keystore")
            //storePassword "your password"
            //keyAlias "your alias"
            //keyPassword "your password"
            //edit local.properties
            //second, add property STORE_FILE, STORE_PASSWORD, KEY_ALIAS, KEY_PASSWORD
        }
    }

    compileSdkVersion 27
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId 'com.xuie.android'
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 10
        versionName "1.3.0"

        flavorDimensions "versionCode"
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding { enabled = true }

    productFlavors {
        Canary {
            manifestPlaceholders = [channel: "Canary"]
        }
    }

    // rename the apk with the version name
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == "release") {
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    def fileName = outputFile.name.replace("app",
                            """android-review-${variant.buildType.name}-v${variant.versionName}-${
                                buildTime()
                            }-${variant.productFlavors[0].name}.apk""".toLowerCase())
                    outputFileName = fileName
                }

            }
        }
    }
}

def Properties props = new Properties()
def propFile = file('../local.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {

        println 'RELEASE BUILD SIGNING'

        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'RELEASE BUILD NOT FOUND SIGNING PROPERTIES'

        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'RELEASE BUILD NOT FOUND SIGNING FILE'
    android.buildTypes.release.signingConfig = null
}

static def buildTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation 'com.android.support:appcompat-v7:27.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:support-v4:27.0.0'
    implementation 'com.android.support:appcompat-v7:27.0.0'
    implementation 'com.android.support:design:27.0.0'
    implementation 'com.android.support:cardview-v7:27.0.0'
    implementation 'com.android.support:support-vector-drawable:27.0.0'
    implementation 'com.android.support:animated-vector-drawable:27.0.0'
    implementation 'com.android.support:palette-v7:27.0.0'

    implementation 'com.jakewharton:butterknife:8.8.1'
    androidTestImplementation 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation 'com.umeng.analytics:analytics:6.1.2'

    implementation 'com.github.bumptech.glide:glide:4.3.1'
    implementation 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'

    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.1.6'

    implementation 'pub.devrel:easypermissions:1.0.0'

    implementation 'com.orhanobut:logger:2.1.1'

    implementation 'com.mikepenz:iconics-core:2.8.2@aar'
    implementation 'com.mikepenz:material-design-iconic-typeface:2.2.0.2@aar'

    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.28'

    implementation 'com.yarolegovich:discrete-scrollview:1.3.0'

    implementation 'com.gongwen:swipeback:1.0.2'
}
