plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

static def releaseTime() {
    return new Date().format("yy.MMddHH", TimeZone.getTimeZone("GMT+8"))
}

def name = "android_preview"
def version = "1." + releaseTime()
def gitCommits = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    signingConfigs {
        def propsFile = rootProject.file('keystore.properties')
        if (propsFile.exists()) {
            def props = new Properties()
            props.load(new FileInputStream(propsFile))
            myConfig {
                storeFile file(props['storeFile'])
                storePassword props['storePassword']
                keyAlias props['keyAlias']
                keyPassword props['keyPassword']
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }

    compileSdkVersion 30

    defaultConfig {
        applicationId 'xuk.android'
        minSdkVersion 26
        targetSdkVersion 30
        versionCode gitCommits
        versionName version

        flavorDimensions "flavor"
    }

    buildTypes {
        release {
            if (rootProject.file('keystore.properties').exists()) {
                signingConfig signingConfigs.myConfig
            }
            debuggable false
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }

    dataBinding {
        enabled = true
    }

    viewBinding {
        enabled = true
    }

    productFlavors {
        Canary {
            manifestPlaceholders = [channel : "Canary",
                                    app_name: "J-Canary",
                                    app_icon: "@drawable/ic_canary_islands"]
        }
        Stage {
            manifestPlaceholders = [channel : "Stage",
                                    app_name: "J-Stage",
                                    app_icon: "@drawable/ic_stage"]
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def flavor = variant.productFlavors[0].name.toLowerCase()
            outputFileName = "${name}_${flavor}_${defaultConfig.versionName}.apk"
        }
    }

}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.3.0'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Kotlin Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    implementation "androidx.preference:preference-ktx:$preference_version"
    implementation 'androidx.paging:paging-runtime-ktx:3.0.0-beta02'

    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    implementation 'pub.devrel:easypermissions:3.0.0'

    // crash && update
    implementation 'com.tencent.bugly:crashreport:3.1.0'
    // 使用调色板 https://developer.android.com/kotlin/ktx?hl=zh-cn#palette
    implementation 'androidx.palette:palette-ktx:1.0.0'

    // log
    implementation 'com.jakewharton.timber:timber:4.7.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}
