apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    signingConfigs {
        release {
            //storeFile file("../yourapp.keystore")
            //storePassword "your password"
            //keyAlias "your alias"
            //keyPassword "your password"
            //edit local.properties
            //second, add property STORE_FILE, STORE_PASSWORD, KEY_ALIAS, KEY_PASSWORD
        }
    }

    compileSdkVersion 28

    defaultConfig {
        applicationId 'com.xuie.android'
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 12
        versionName "1.3.1"

        flavorDimensions "versionCode"
    }

    buildTypes {
        release {
            buildConfigField "boolean", "D", "false"
            debuggable false
            signingConfig signingConfigs.release
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField "boolean", "D", "true"
            debuggable true
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding { enabled = true }

    productFlavors {
        Canary {
            manifestPlaceholders = [channel : "Canary",
                                    app_name: "AndroidReview测试版",
                                    app_icon: "@drawable/ic_launcher"]
        }
        Stage {
            manifestPlaceholders = [channel : "Stage",
                                    app_name: "AndroidReview标准版",
                                    app_icon: "@drawable/ic_stage"]
        }
    }

    // rename the apk with the version name
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == "release") {
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    def fileName = outputFile.name.replace("app", "android-v${variant.versionName}-${variant.versionCode}-${buildTime()}".toLowerCase())
                    outputFileName = fileName
                }

            }
        }
    }
}

def Properties props = new Properties()
def propFile = file('../local.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {

        println 'RELEASE BUILD SIGNING'
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'RELEASE BUILD NOT FOUND SIGNING PROPERTIES'

        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'RELEASE BUILD NOT FOUND SIGNING FILE'
    android.buildTypes.release.signingConfig = null
}

static def buildTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'androidx.appcompat:appcompat:1.1.0-alpha03'
    api 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    api 'androidx.room:room-runtime:2.1.0-alpha06'
    kapt 'androidx.room:room-compiler:2.1.0-alpha06'
    api 'androidx.paging:paging-runtime-ktx:2.1.0'
    api 'com.google.android.material:material:1.0.0'
    api 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    // Navigation
    api 'androidx.navigation:navigation-fragment:2.1.0-alpha01'
    api 'androidx.navigation:navigation-runtime:2.1.0-alpha01'
    api 'androidx.navigation:navigation-ui:2.1.0-alpha01'

    api 'com.github.bumptech.glide:glide:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'

    api 'pub.devrel:easypermissions:3.0.0'
    api 'com.orhanobut:logger:2.2.0'
    api 'com.mikepenz:iconics-core:3.0.3@aar'
    api 'com.mikepenz:material-design-iconic-typeface:2.2.0.4@aar'
    api 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.46'
    api 'com.yarolegovich:discrete-scrollview:1.3.0'
    api 'com.gongwen:swipeback:1.0.2'
    api 'com.tencent.bugly:crashreport:2.8.6'
    api 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0-alpha-14'
    api 'androidx.palette:palette:1.0.0'

    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api 'androidx.core:core-ktx:1.1.0-alpha05'
}
repositories {
    mavenCentral()
}
